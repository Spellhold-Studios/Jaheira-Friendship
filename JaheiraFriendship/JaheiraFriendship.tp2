BACKUP ~weidu_external/JaheiraFriendship/backup~
SUPPORT ~https://github.com/Spellhold-Studios/Jaheira-Friendship~ // AUTHOR ~Lava Del'Vortel~
VERSION ~v1.2~
README ~JaheiraFriendship/JaheiraFriendship-readme-%LANGUAGE%.html~ ~JaheiraFriendship/JaheiraFriendship-readme-english.html~

ALWAYS
  ACTION_IF (!VARIABLE_IS_SET l#_always_done) BEGIN // run only once per install
    // convert strings from UTF-8 for original games
    ACTION_DEFINE_ARRAY l#_noconv BEGIN setup END

    LAF HANDLE_CHARSETS
      INT_VAR
        infer_charsets    = 1
        from_utf8         = 1
      STR_VAR
        default_language  = ~english~
        tra_path          = ~JaheiraFriendship/translations~
        out_path          = ~weidu_external/JaheiraFriendship/translations~
        noconvert_array   = ~l#_noconv~
    END

    ACTION_IF (GAME_IS ~bg2ee eet~) BEGIN
      OUTER_TEXT_SPRINT tra_location ~JaheiraFriendship/translations~
    END ELSE BEGIN
      OUTER_TEXT_SPRINT tra_location ~weidu_external/JaheiraFriendship/translations~
    END

    OUTER_SET l#_always_done = 1
  END
END

AUTO_TRA ~%tra_location%/%s~

LANGUAGE ~English~
         ~English~
         ~JaheiraFriendship/translations/english/setup.tra~

BEGIN @1
DESIGNATED 0 LABEL ~jaheira_friendship~

/* CamDawg's custom IsValidForPartyDialogue() state. Thanks Cam! covers STATE_CONFUSED, STATE_FEEBLEMINDED, STATE_SILENCED, STATE_DEAD, STATE_ACID_DEATH, STATE_FLAME_DEATH, STATE_EXPLODING_DEATH, STATE_STONE_DEATH, STATE_FROZEN_DEATH, STATE_HELPLESS, STATE_STUNNED, STATE_PANIC, STATE_BERSERK, STATE_SLEEPING */
   
APPEND ~STATE.IDS~ ~0x80101FEF CD_STATE_NOTVALID~ UNLESS ~CD_STATE_NOTVALID~

// Minor SoA fix

COPY_EXISTING ~jaheira.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~RealSetGlobalTimer("JaheiraRomance","GLOBAL",FIVE_DAYS)~ ~RealSetGlobalTimer("JaheiraRomance","GLOBAL",3600)~ 
END
BUT_ONLY

// Compiling dialogue & scripts SOA
COMPILE ~JaheiraFriendship/dialogue/jaheiraj.d~
EXTEND_TOP ~jaheira.bcs~  ~JaheiraFriendship/scripts/jaheira.baf~
EXTEND_TOP ~jaheirad.bcs~ ~JaheiraFriendship/scripts/jaheirad.baf~
EXTEND_TOP ~AR0300.bcs~   ~JaheiraFriendship/scripts/AR0300.baf~

// Compiling dialogue & scripts TOB
COMPILE ~JaheiraFriendship/dialogue/jahei25j.d~
EXTEND_TOP ~jahe25.bcs~ ~JaheiraFriendship/scripts/jahe25.baf~
